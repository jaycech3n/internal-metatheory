Cosieves in countably simple semicategories
===========================================

\begin{code}

{-# OPTIONS --without-K --rewriting #-}

open import reedy.SimpleSemicategories

module reedy.Cosieves {â„“â‚˜} (I : SimpleSemicategory â„“â‚˜) where

open SimpleSemicategory I

\end{code}


Shapes of linear cosieves
-------------------------

\begin{code}

shape : â„• â†’ â„• â†’ â„• â†’ Typeâ‚€
shape i h t = t â‰¤ hom-size i h

prev-shape : âˆ€ {i h t} â†’ shape i h (1+ t) â†’ shape i h t
prev-shape = Sâ‰¤-â‰¤

full-shape : âˆ€ i h â†’ shape i h (hom-size i h)
full-shape i h = lteE

total-shape-1+ : âˆ€ i â†’ shape (1+ i) i (hom-size (1+ i) i)
total-shape-1+ i = full-shape (1+ i) i

<-to-shape : âˆ€ {i h t} â†’ t < hom-size i h â†’ shape i h t
<-to-shape = inr

<-from-shape : âˆ€ {i h t} â†’ shape i h (1+ t) â†’ t < hom-size i h
<-from-shape = Sâ‰¤-<

Shape = Î£[ i ï¹• â„• ] Î£[ h ï¹• â„• ] Î£[ t ï¹• â„• ] shape i h t

ð‘– : Shape â†’ â„•
ð‘– = fst

â„Ž : Shape â†’ â„•
â„Ž = fst âˆ˜ snd

ð‘¡ : Shape â†’ â„•
ð‘¡ = 2nd âˆ˜ snd

is-shape : ((i , h , t , _) : Shape) â†’ shape i h t
is-shape = 3rd âˆ˜ snd

shape-is-prop : âˆ€ {i h t} â†’ is-prop (shape i h t)
shape-is-prop = â‰¤-is-prop

shape-path : âˆ€ {i h t} (s s' : shape i h t) â†’ s == s'
shape-path = prop-has-all-paths

\end{code}


Counting factors
----------------

\begin{code}

discrim : âˆ€ i h t u â†’ âˆ€ {j} (f : hom i j) â†’ Dec (f âˆ£ #[ t ] i h u)
discrim i h t u f = f âˆ£? #[ t ] i h u

count-factors :
  âˆ€ i h t
  â†’ shape i h t
  â†’ âˆ€ {j} â†’ hom i j
  â†’ â„•
count-factors-aux :
  âˆ€ i h t
  â†’ (u : t < hom-size i h)
  â†’ âˆ€ {j} (f : hom i j)
  â†’ Dec (f âˆ£ (#[ t ] i h u))
  â†’ â„•

count-factors i h O s f = O
count-factors i h (1+ t) s f =
  let u = <-from-shape s in
  count-factors-aux i h t u f $ discrim i h t u f

count-factors-aux i h t u f (inr no) =
  count-factors i h t (<-to-shape u) f
count-factors-aux i h t u f (inl yes) =
  1+ (count-factors i h t (<-to-shape u) f)

\end{code}

* The following is Lemma 6.22 of the paper (as of 31.01.2024).

\begin{code}

count-factors-top-level : -- 6.22
  âˆ€ i h t s (f : hom i h) â†’ count-factors i h t s f == O
count-factors-top-level-aux :
  âˆ€ i h t u f d â†’ count-factors-aux i h t u f d == O

count-factors-top-level i h O s f = idp
count-factors-top-level i h (1+ t) s f =
  let u = <-from-shape s in
  count-factors-top-level-aux i h t u f $ discrim i h t u f

count-factors-top-level-aux i h t u f (inl (g , _)) =
  âŠ¥-rec (endo-hom-empty g)
count-factors-top-level-aux i h t u f (inr no) =
  count-factors-top-level i h t (<-to-shape u) f

\end{code}

Basic properties of `count-factors`.

\begin{code}

count-factors-divisible-aux :
  âˆ€ i h t u {j} (f : hom i j) d
  â†’ f âˆ£ #[ t ] i h u
  â†’ count-factors-aux i h t u f d == 1+ (count-factors i h t (<-to-shape u) f)
count-factors-divisible-aux i h t u f (inl _) yes = idp
count-factors-divisible-aux i h t u f (inr no) yes = âŠ¥-rec $ no yes

count-factors-divisible :
  âˆ€ i h t s {j} (f : hom i j)
  â†’ f âˆ£ #[ t ] i h (<-from-shape s)
  â†’ count-factors i h (1+ t) s f == 1+ (count-factors i h t (prev-shape s) f)
count-factors-divisible i h t s f yes =
  let u = <-from-shape s in
  count-factors-divisible-aux i h t u f (discrim i h t u f) yes

\end{code}

* Proposition 6.24 (31.01.2024)

\begin{code}

module _ -- 6.24
  (i h : â„•) {j : â„•} (f : hom i j)
  ((tâ‚€ , uâ‚€) : Fin (hom-size i h))
  (divisible : f âˆ£ #[ tâ‚€ ] i h uâ‚€)
  (smallest : (âˆ€ t u â†’ f âˆ£ #[ t ] i h u â†’ tâ‚€ â‰¤ t))
  where

  count-factors-below-first-divisible :
    âˆ€ t s â†’ t â‰¤ tâ‚€ â†’ count-factors i h t s f == O
  count-factors-below-first-divisible-aux :
    âˆ€ t u d â†’ 1+ t â‰¤ tâ‚€ â†’ count-factors-aux i h t u f d == O

  count-factors-below-first-divisible O s w = idp
  count-factors-below-first-divisible (1+ t) s w =
    let u = <-from-shape s in
    count-factors-below-first-divisible-aux t u (discrim i h t u f) w

  count-factors-below-first-divisible-aux t u (inl yes) w =
    âŠ¥-rec $ Sâ‰° $ â‰¤-trans w $ smallest _ _ yes
  count-factors-below-first-divisible-aux t u (inr no) w =
    count-factors-below-first-divisible t (<-to-shape u) (Sâ‰¤-â‰¤ w)

\end{code}

* Lemma 6.25 (31.01.2024)

\begin{code}

module _ (i h : â„•) {j} (f : hom i j) where -- 6.25
  count-factors-all-O-hom-size-O :
    (âˆ€ t s â†’ count-factors i h t s f == O)
    â†’ hom-size j h == O
  count-factors-all-O-hom-size-O all-O = Â¬O<-=O (hom-size j h) bot
    where module _ (O<homjh : O < hom-size j h) where
      [O] = #[ O ] j h O<homjh
      tâ‚€ = idx ([O] â—¦ f)
      uâ‚€  = idx<hom-size ([O] â—¦ f)
      sâ‚€ = <-Sâ‰¤ uâ‚€

      p : count-factors i h (1+ tâ‚€) sâ‚€ f == 1+ (count-factors i h tâ‚€ _ f)
      p = count-factors-divisible i h tâ‚€ sâ‚€ f (âˆ£â—¦hom#-idx f [O])

      bot : âŠ¥
      bot = â„•-Oâ‰ S _ (! (all-O (1+ tâ‚€) sâ‚€) âˆ™ p)

  hom-size-O-no-divisible :
    hom-size j h == O
    â†’ âˆ€ t u â†’ Â¬ (f âˆ£ #[ t ] i h u)
  hom-size-O-no-divisible p t u (g , _) =
    â‰®O _ $ transp (O <_) p $ hom[ j , h ]-inhab g

  no-divisible-count-factors-all-O :
    (âˆ€ t u â†’ Â¬ (f âˆ£ #[ t ] i h u))
    â†’ âˆ€ t s â†’ count-factors i h t s f == O
  no-divisible-count-factors-all-O-aux :
    (âˆ€ t u â†’ Â¬ (f âˆ£ #[ t ] i h u))
    â†’ âˆ€ t u d â†’ count-factors-aux i h t u f d == O

  no-divisible-count-factors-all-O no-div O s = idp
  no-divisible-count-factors-all-O no-div (1+ t) s =
    let u = <-from-shape s in
    no-divisible-count-factors-all-O-aux no-div t u $ discrim i h t u f

  no-divisible-count-factors-all-O-aux no-div t u (inl yes) =
    âŠ¥-rec $ no-div _ _ yes
  no-divisible-count-factors-all-O-aux no-div t u (inr no) =
    no-divisible-count-factors-all-O no-div t (<-to-shape u)

  -- Lots of annoying finagling to the right form in this.
  -- Could probably do it more concisely.
  hom-size>O-exists-divisible :
    O < hom-size j h
    â†’ Î£ (Fin (hom-size i h)) Î» (t , u) â†’ f âˆ£ #[ t ] i h u
  hom-size>O-exists-divisible O<homjh =
    Â¬âˆ€FinÂ¬ _ _ (Î» (t , u) â†’ f âˆ£? #[ t ] i h u)
      $ Â¬uncurry $ c $ â‰ -inv $ <-to-â‰  O<homjh
    where
    no-divisible-hom-size-O =
      count-factors-all-O-hom-size-O âˆ˜ no-divisible-count-factors-all-O
    c = contrapos no-divisible-hom-size-O

\end{code}


Division by morphisms
---------------------

Assume that I is strictly oriented, and that i, h, j : Iâ‚€ such that
  | I(j, h) | > 0,
with f : I(i, j). This means that there is a smallest index
  (tâ‚€, uâ‚€) : Fin | I(i, h) |
such that f âˆ£ [tâ‚€].

\begin{code}

module Cosieves-StrictlyOriented
  (I-strictly-oriented : is-strictly-oriented I)
  where

  open SimpleSemicategories-IsStrictlyOriented I I-strictly-oriented

  module _ (i h j : â„•) (f : hom i j) (O<homjh : O < hom-size j h) where

    O<homih : O < hom-size i h
    O<homih = hom[ i , h ]-inhab (#[ O ] j h O<homjh â—¦ f)

    smallest-divisible :
      Î£ (Fin (hom-size i h)) (is-smallest-Fin (Î» (t , u) â†’ f âˆ£ #[ t ] i h u))
    smallest-divisible =
      uncurry (Fin-smallest-witness (Î» (t , u) â†’ f âˆ£? #[ t ] i h u))
        $ hom-size>O-exists-divisible i h f O<homjh

    tâ‚€ = to-â„• $ fst smallest-divisible
    uâ‚€ = snd $ fst smallest-divisible
    tâ‚€-divisible = 2nd smallest-divisible
    tâ‚€-smallest = curry (3rd smallest-divisible)

\end{code}

Now define division by f.

\begin{code}

    divby : âˆ€ t â†’ t < hom-size i h â†’ hom j h
    divby-aux : âˆ€ t (u : t < hom-size i h) â†’ Dec (f âˆ£ #[ t ] i h u) â†’ hom j h

    divby t u = divby-aux t u $ discrim i h t u f

    divby-aux t u (inl (g , _)) = g
    divby-aux O u (inr no) = #[ O ] j h O<homjh
    divby-aux (1+ t) u (inr no) = divby t (S<-< u)

\end{code}

A few basic observations about `divby`:

\begin{code}

    divby-value-aux :
      âˆ€ t u d g
      â†’ g â—¦ f == #[ t ] i h u
      â†’ divby-aux t u d == g
    divby-value-aux t u (inl (_ , q)) g p = hom-is-epi _ _ _ (q âˆ™ ! p)
    divby-value-aux t u (inr no) g p = âŠ¥-rec $ no (g , p)

    divby-value :
      âˆ€ t u g
      â†’ g â—¦ f == #[ t ] i h u
      â†’ divby t u == g
    divby-value t u = divby-value-aux t u (discrim i h t u f)

    divby-divisible-â—¦-aux :
      âˆ€ t u d â†’ f âˆ£ #[ t ] i h u â†’ divby-aux t u d â—¦ f == #[ t ] i h u
    divby-divisible-â—¦-aux t u (inl (_ , p)) yes = p
    divby-divisible-â—¦-aux t u (inr no) yes = âŠ¥-rec $ no yes

    divby-divisible-â—¦ :
      âˆ€ t u â†’ f âˆ£ #[ t ] i h u â†’ divby t u â—¦ f == #[ t ] i h u
    divby-divisible-â—¦ t u = divby-divisible-â—¦-aux t u $ discrim i h t u f

\end{code}

* Lemma 6.26 (31.01.2024)

\begin{code}
    divby-is-lub : -- 6.26
      âˆ€ t u g
      â†’ g â—¦ f â‰¼ #[ t ] i h u
      â†’ g â‰¼ divby t u
    divby-is-lub-aux :
      âˆ€ t u d g
      â†’ g â—¦ f â‰¼ #[ t ] i h u
      â†’ g â‰¼ divby-aux t u d

    divby-is-lub t u = divby-is-lub-aux t u $ discrim i h t u f

    divby-is-lub-aux O u d g w =
      =-â‰¼ (! (divby-value-aux _ _ d _ (â‰¼[O] _ _ w)))
    divby-is-lub-aux (1+ t) u (inl (g' , p)) g w =
      â‰¼-cancel-r _ _ _ (transp (_ â‰¼_) (! p) w)
    divby-is-lub-aux (1+ t) u (inr no) g (inl p) =
      âŠ¥-rec $ no (g , idx=-hom= p)
    divby-is-lub-aux (1+ t) u (inr no) g (inr w) =
      divby-is-lub t (S<-< u) g (â‰º#S-â‰¼# _ _ w)

\end{code}

* Lemma 6.27 (01.02.2024)

\begin{code}

    divby-<-smallest-divisible :
      âˆ€ t u â†’ t < tâ‚€ â†’ divby t u == #[ O ] j h O<homjh
    divby-<-smallest-divisible-aux :
      âˆ€ t u d â†’ t < tâ‚€ â†’ divby-aux t u d == #[ O ] j h O<homjh

    divby-<-smallest-divisible t u =
      divby-<-smallest-divisible-aux t u $ discrim i h t u f

    divby-<-smallest-divisible-aux t u (inl yes) v =
      âŠ¥-rec $ Â¬<-self $ <-â‰¤-< v (tâ‚€-smallest _ _ yes)
    divby-<-smallest-divisible-aux O u (inr no) v = idp
    divby-<-smallest-divisible-aux (1+ t) u (inr no) v =
      divby-<-smallest-divisible t (S<-< u) (S<-< v)

    divby-smallest-divisible : divby tâ‚€ uâ‚€ == #[ O ] j h O<homjh
    divby-smallest-divisible = â‰¼[O] O<homjh _ (â‰¼-cancel-r _ _ _ w')
      where
      [O]â—¦f = #[ O ] j h O<homjh â—¦ f
      [tâ‚€] = #[ tâ‚€ ] i h uâ‚€

      iâ‚€ = idx [O]â—¦f
      vâ‚€ = idx<hom-size [O]â—¦f

      p : divby tâ‚€ uâ‚€ â—¦ f == [tâ‚€]
      p = divby-divisible-â—¦ tâ‚€ uâ‚€ tâ‚€-divisible

      -- Wouldn't need all this idx/hom# wrangling with
      -- a more definitional representation of arrows.
      w : [tâ‚€] â‰¼ [O]â—¦f
      w = tâ‚€-smallest iâ‚€ vâ‚€ (âˆ£â—¦hom#-idx f _) â—‚$ transp! (_â‰¤ iâ‚€) (idx-hom# _)

      w' : divby tâ‚€ uâ‚€ â—¦ f â‰¼ [O]â—¦f
      w' = â‰¼-trans (=-â‰¼ p) w

    divby-â—¦-ub :
      âˆ€ t u â†’ tâ‚€ â‰¤ t â†’ divby t u â—¦ f â‰¼ #[ t ] i h u
    divby-â—¦-ub-aux :
      âˆ€ t u d â†’ tâ‚€ â‰¤ t â†’ divby-aux t u d â—¦ f â‰¼ #[ t ] i h u

    divby-â—¦-ub t u = divby-â—¦-ub-aux t u $ discrim i h t u f

    divby-â—¦-ub-aux t u d (inl idp) =
      =-â‰¼ (divby-divisible-â—¦-aux t u d (âˆ£#[]= tâ‚€-divisible))
    divby-â—¦-ub-aux t u (inl yes) (inr v) = =-â‰¼ (snd yes)
    divby-â—¦-ub-aux (1+ t) u (inr no) (inr v) =
      â‰¼-â‰º-â‰¼ (divby-â—¦-ub t w  (<S-â‰¤ v)) (#[ t ]â‰ºS w u)
      where w = S<-< u

\end{code}

* Lemma 6.28 (01.02.2024)

Morphism division is monotone.

\begin{code}

    divby-<-monotone :
      âˆ€ {t t'} {u u'}
      â†’ t < t' â†’ divby t u â‰¼ divby t' u'
    divby-<-monotone {t} .{1+ t} {u} {u'} ltS =
      case (â„•-trichotomy' tâ‚€ t) case[tâ‚€â‰¤t] case[t<tâ‚€]
      where
      case[tâ‚€â‰¤t] = Î» tâ‚€â‰¤t â†’
        divby-is-lub (1+ t) _ _ $ â‰¼-â‰º-â‰¼ (divby-â—¦-ub t _ tâ‚€â‰¤t) (#[ t ]â‰ºS _ _)
      case[t<tâ‚€] = Î» t<tâ‚€ â†’
        [O]-min _ _
          â—‚$ transp! (_â‰¼ _) (divby-<-smallest-divisible _ _ t<tâ‚€)
    divby-<-monotone {t} {1+ t'} {u} {u'} (ltSR w) =
      â‰¼-trans
        (divby-<-monotone {t} {t'} {u} {S<-< u'} w)
        (divby-<-monotone {t'} {1+ t'} ltS)

    divby-â‰¤-monotone :
      âˆ€ {t t'} {u u'}
      â†’ t â‰¤ t' â†’ divby t u â‰¼ divby t' u'
    divby-â‰¤-monotone (inl idp) = =-â‰¼ (ap (divby _) (<-has-all-paths _ _))
    divby-â‰¤-monotone (inr w) = divby-<-monotone w

    divby-reflects-<-monotone :
      âˆ€ {t t'} {u u'}
      â†’ divby t u â‰º divby t' u'
      â†’ t < t'
    divby-reflects-<-monotone w = â‰°-to-> Î» c â†’ â‰¤-to-â‰¯ (divby-â‰¤-monotone c) w

\end{code}

* Proposition 6.29 (01.02.2024)

Morphism division is surjective.

\begin{code}

    divby-surj : (g : hom j h) â†’ divby (idx (g â—¦ f)) (idx<hom-size (g â—¦ f)) == g
    divby-surj g =
      divby-value (idx (g â—¦ f)) (idx<hom-size (g â—¦ f)) g (! (hom#-idx _))

\end{code}

* Corollary 6.30 (01.02.2024)

Upper bound on idx([t+1]/f).

\begin{code}

    idx-divby-S-upper-bound :
      (t : â„•) (u : 1+ t < hom-size i h)
      â†’ idx (divby (1+ t) u) â‰¤ 1+ (idx (divby t (S<-< u)))
    idx-divby-S-upper-bound t u =
      case (<-Sâ‰¤ m<homjh) case[m+1=homjh] case[m+1<homjh]
      where
      [t]/f = divby t (S<-< u)
      m = idx [t]/f
      m<homjh = idx<hom-size [t]/f

      [t+1]/f = divby (1+ t) u
      n = idx [t+1]/f
      n<homjh = idx<hom-size [t+1]/f

      case[m+1=homjh] = Î» m+1=homjh â†’ inr (transp! (n <_) m+1=homjh n<homjh)

      case[m+1<homjh] = Î» m+1<homjh â†’ â‰®-to-â‰¥ (bot m+1<homjh)
        where module _ (b : 1+ m < hom-size j h) (c : 1+ m < n) where
        t' = idx (#[ 1+ m ] j h b â—¦ f)
        u' = idx<hom-size (#[ 1+ m ] j h b â—¦ f)

        p : #[ 1+ m ] j h b == divby t' u'
        p = ! (divby-surj _)

        v : [t]/f â‰º divby t' u'
        v = #[ m ]â‰ºS m<homjh b
               â—‚$ transp (_â‰º #[ 1+ m ] j h b) (hom#-idx _)
               â—‚$ transp ([t]/f â‰º_) p

        v' : divby t' u' â‰º [t+1]/f
        v' = transp (_< _) (! (idx-hom# _) âˆ™ ap idx p) c

        w : t < t'
        w = divby-reflects-<-monotone v

        w' : t' < 1+ t
        w' = divby-reflects-<-monotone v'

        bot : âŠ¥
        bot = no-between w w'

\end{code}

    divby-1+â‰¼-divby-to-= :
      âˆ€ {t} {u} {v}
      â†’ divby (1+ t) u â‰¼ divby t v
      â†’ divby (1+ t) u == divby t v
    divby-1+â‰¼-divby-to-= (inl p) = idx=-hom= p
    divby-1+â‰¼-divby-to-= (inr w) = âŠ¥-rec $ Sâ‰® $ divby-reflects-monotone _ _ w

    idx-divby-1+-divisible :
      (t : â„•) (u : 1+ t < hom-size i h)
      â†’ tâ‚€ â‰¤ t
      â†’ f âˆ£ #[ 1+ t ] i h u
      â†’ idx (divby (1+ t) u) == 1+ (idx (divby t (S<-< u)))
    idx-divby-1+-divisible t u v d with f âˆ£? #[ 1+ t ] i h u
    ... | inr no = âŠ¥-rec $ no d
    ... | inl (g , p)
          with idx-divby-1+-upper-bound t u
    ...   | inl q = (ap idx $ ! (divby-value p)) âˆ™ q
    ...   | inr w = âŠ¥-rec $ Sâ‰° contra
            where
            r : divby (1+ t) u == divby t (S<-< u)
            r = divby-1+â‰¼-divby-to-= (<S-â‰¤ w)

            c : #[ 1+ t ] i h u â‰¼ #[ t ] i h (S<-< u)
            c = divby-â—¦-ub t _ v
                â—‚$ transp! (Î» â—» â†’ â—» â—¦ f â‰¼ #[ t ] i h _) r
                â—‚$ transp (_â‰¼ #[ t ] i h (S<-< u)) (divby-divisible-â—¦ _ _ d)

            contra : 1+ t â‰¤ t
            contra = c â—‚$ transp (idx (#[ 1+ t ] i h _) â‰¤_) (idx-hom# t)
                       â—‚$ transp (_â‰¤ t) (idx-hom# (1+ t))

    -- 6.32
    abstract
      count-factors-idx-divby :
        (t : â„•) (u : t < hom-size i h) (s : shape i h (1+ t))
        â†’ tâ‚€ â‰¤ t
        â†’ count-factors i h (1+ t) s f == 1+ (idx (divby t u))
      count-factors-idx-divby t u s (inl idp) = p âˆ™ ap 1+ (q âˆ™ ! r)
        where
        p : count-factors i h (1+ tâ‚€) s f
            == 1+ (count-factors i h tâ‚€ (prev-shape s) f)
        p = count-factors-divisible tâ‚€ s (âˆ£#[]= tâ‚€-divisible)

        q : count-factors i h tâ‚€ (prev-shape s) f == O
        q = count-factors-O-below-first-divisible tâ‚€ lteE

        r : idx (divby tâ‚€ u) == O
        r = hom=-idx= (ap (divby tâ‚€) (<-has-all-paths _ _) âˆ™ divby-smallest-divisible)
            âˆ™ idx-hom# _
      count-factors-idx-divby (1+ t) u s (inr w)
       with count-factors-discrim i h (1+ t) (Sâ‰¤-< s) f
          | divby-discrim (1+ t) u
      ... | inl yes | inl yes'@(g , p)
            =
            q âˆ™ ap 1+ (! (idx-divby-1+-divisible t u (<S-â‰¤ w) yes')
                       âˆ™ ap idx (divby-value p))
            where
            q : count-factors-aux i h (1+ t) (Sâ‰¤-< s) f (inl yes)
                == 2+ (idx (divby t (S<-< u)))
            q = ap 1+
                  (count-factors-idx-divby t (S<-< u) (prev-shape s) (<S-â‰¤ w))
      ... | inr no | inr no' =
              count-factors-idx-divby t (S<-< u) (prev-shape s) (<S-â‰¤ w)
      ... | inl yes | inr no' = âŠ¥-rec $ no' (âˆ£#[]= yes)
      ... | inr no | inl yes' = âŠ¥-rec $ no (âˆ£#[]= yes')

  -- module 6âˆ™33 where -- paper version 26.01.24
  --   -- Deviates slightly from paper proof.
  --   count-factors-shape :
  --     âˆ€ i h t s {j} (f : hom i j)
  --     â†’ count-factors i h t s f â‰¤ hom-size j h
  --   count-factors-shape[_,_,1+_] :
  --     âˆ€ i h t u {j} (f : hom i j) d
  --     â†’ count-factors[ i , h ,1+ t ] u f d â‰¤ hom-size j h

  --   count-factors-shape i h O s f = Oâ‰¤ _
  --   count-factors-shape i h (1+ t) s f =
  --     let u = Sâ‰¤-< s in
  --     count-factors-shape[ i , h ,1+ t ] u f (count-factors-discrim[1+ t ] u f)

  --   count-factors-shape[ i , h ,1+ t ] u f (inl yes) = {!!}
  --   count-factors-shape[ i , h ,1+ t ] u f (inr no) =
  --     count-factors-shape i h t (<-shape u) f

  --   private -- experimental; unused
  --     record Shape-helper (i h t : â„•) â¦ƒ s : shape i h t â¦„ : Typeâ‚€  where
  --       constructor _,_
  --       field
  --         dt : â„•
  --         eq : dt == hom-size i h âˆ’ t

  -- open 6âˆ™33 public

  -- module 6âˆ™23 where -- version 17.01.24
  --   count-factors-full :
  --     âˆ€ i h s {j} (f : hom i j)
  --     â†’ count-factors i h (hom-size i h) s f == hom-size j h
  --   count-factors-full = {!!}

  -- open 6âˆ™23 public

  -- -- Need this too; prove it on paper:
  -- count-factors-comp :
  --   âˆ€ i h t s {j} (f : hom i j) {k} (g : hom j k)
  --   â†’ âˆ€ {s'}
  --   â†’ count-factors i h t s (g â—¦ f)
  --     == count-factors j h (count-factors i h t s f) s' g
  -- count-factors-comp[_,_,1+_] :
  --   âˆ€ i h t u {j} (f : hom i j) {k} (g : hom j k)
  --   â†’ (d : Dec (g â—¦ f âˆ£ #[ t ] i h u))
  --   â†’ âˆ€ {s'}
  --   â†’ count-factors[ i , h ,1+ t ] u (g â—¦ f) d
  --     == count-factors j h (count-factors[ i , h ,1+ t ] u f {!!}) s' g

  -- count-factors-comp i h O s f g = idp
  -- count-factors-comp i h (1+ t) s f g =
  --   count-factors-comp[ i , h ,1+ t ] u f g (g â—¦ f âˆ£? #[ t ] i h u)
  --   where u = Sâ‰¤-< s

  -- count-factors-comp[ i , h ,1+ t ] u f g (inl yes) = {!!}
  -- count-factors-comp[ i , h ,1+ t ] u f g (inr no) = {!!}
